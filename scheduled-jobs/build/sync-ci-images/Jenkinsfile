// Monitor all branches of ocp-build-data

@NonCPS
def sortedVersions() {
    return commonlib.ocp4Versions.sort(false)
}

node {



    checkout scm
    def buildlib = load("pipeline-scripts/buildlib.groovy")
    def commonlib = buildlib.commonlib

    properties([
        disableConcurrentBuilds(),
        disableResume(),
        buildDiscarder(
            logRotator(
                artifactDaysToKeepStr: '20',
                daysToKeepStr: '20')),
        [
            $class: 'ParametersDefinitionProperty',
            parameterDefinitions: [
                string(
                    name: "ONLY_RELEASE",
                    description: "Only run for one version; e.g. 4.7",
                    defaultValue: "",
                    trim: true,
                ),
                string(
                    name: "ADD_LABELS",
                    description: "Space delimited list of labels to add to existing/new PRs",
                    defaultValue: "",
                    trim: true,
                ),
                string(
                        name: 'ASSEMBLY',
                        description: 'The name of an assembly to rebase & build for. If assemblies are not enabled in group.yml, this parameter will be ignored',
                        defaultValue: "stream",
                        trim: true,
                ),
                [
                    name: 'SKIP_WAITS',
                    description: 'Skip sleeps',
                    $class: 'BooleanParameterDefinition',
                    defaultValue: false
                ],
                [
                    name: 'FORCE_RUN',
                    description: 'Run even if ocp-build-data appears unchanged',
                    $class: 'BooleanParameterDefinition',
                    defaultValue: false
                ],
                commonlib.dryrunParam(),
                commonlib.mockParam(),
            ],
        ]
    ])

    if (params.ONLY_RELEASE) {
        for_versions = [ONLY_RELEASE]
    } else {
        for_versions = sortedVersions()
    }

    if (params.FORCE_RUN) {
        currentBuild.displayName += " (forced)"
    }

    for ( String version : for_versions ) {
        group = "openshift-${version}"
        echo "Checking group: ${group}"
        (major, minor) = commonlib.extractMajorMinorVersionNumbers(version)

        sh "rm -rf ${group}"
        sh "git clone https://github.com/openshift/ocp-build-data --branch ${group} --single-branch --depth 1 ${group}"
        dir(group) {
            now_hash = sh(returnStdout: true, script: "git rev-parse HEAD").trim()
        }

        prev_dir_name = "${group}-prev"
        dir(prev_dir_name) {  // if there was a previous, it should be here
            prev_hash = sh(returnStdout: true, script: "git rev-parse HEAD || echo 0").trim()
        }

        echo "Current hash: ${now_hash} "
        echo "Previous hash: ${prev_hash}"

        if (now_hash != prev_hash || params.FORCE_RUN) {
            echo "Changes detected in ocp-build-data group: ${group}"
            currentBuild.displayName += " ${version}"

            doozerWorking = "${env.WORKSPACE}/wd-${version}"
            rc = 0
            try{
                sshagent(["openshift-bot"]) {
                    withEnv(['KUBECONFIG=/home/jenkins/kubeconfigs/art-publish.app.ci.kubeconfig']) {
                        sh "rm -rf ${doozerWorking}"
                        buildlib.doozer("--working-dir ${doozerWorking} --group ${group} images:streams gen-buildconfigs -o ${group}.yaml --apply")
                        sh "oc registry login"
                        buildlib.doozer("--working-dir ${doozerWorking} --group ${group} images:streams mirror --only-if-missing")
                        buildlib.doozer("--working-dir ${doozerWorking} --group ${group} images:streams start-builds")

                        if (!params.SKIP_WAITS) {
                            // Allow the builds to run for about 20 minutes
                            sleep time: 20, unit: 'MINUTES'
                        }
                        // Print out status of builds for posterity
                        buildlib.doozer("--working-dir ${doozerWorking} --group ${group} images:streams check-upstream")
                        withCredentials([string(credentialsId: 'openshift-bot-token', variable: 'GITHUB_TOKEN')]) {
                            if ( (major == 4 && minor >= 6) || major > 4 ) {
                                other_args = '--add-label "bugzilla/valid-bug" --add-label "cherry-pick-approved"'
                                for ( label in params.ADD_LABELS.split() ) {
                                    other_args += " --add-label '${label}'"
                                }
                                // Only open PRs on >= 4.6 to leave well enough alone.
                                rc = sh script:"${buildlib.DOOZER_BIN} --assembly stream --working-dir ${doozerWorking} --group ${group} images:streams prs open --interstitial 840 --add-auto-labels ${other_args} --github-access-token ${GITHUB_TOKEN}", returnStatus: true
                                // rc=25 is used to indicate PR openings were skipped and to try again later.
                                if ( rc != 0 && rc != 25) {
                                    error("Error opening PRs for ${group}: ${rc}")
                                }
                            }
                        }
                    }
                }
            } finally {
                commonlib.safeArchiveArtifacts([
                    "wd-${version}/*.log",
                ])
                sh "rm -rf ${doozerWorking}"
            }

            if ( rc == 25 ) {
                currentBuild.result = "UNSTABLE"
                currentBuild.displayName += "-Partial"
                echo "Some PRs were skipped because parent PRs have not merged yet: ${version}."
                // Do not "mv ${group} ${prev_dir_name}" because we are not done
            } else {
                sh "rm -rf ${prev_dir_name}"
                sh "mv ${group} ${prev_dir_name}"
            }

        } else {
            echo "NO changes detected in ocp-build-data group: ${group}"
        }
    }

    buildlib.cleanWorkspace()
}
