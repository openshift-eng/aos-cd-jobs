#!/usr/bin/env groovy

node {
    checkout scm
    def buildlib = load("pipeline-scripts/buildlib.groovy")
    def commonlib = buildlib.commonlib

    properties(
        [
            buildDiscarder(
                logRotator(
                    artifactDaysToKeepStr: '',
                    artifactNumToKeepStr: '',
                    daysToKeepStr: '',
                    numToKeepStr: '90'
                )
            ),
            [
                $class : 'ParametersDefinitionProperty',
                parameterDefinitions: [
                    commonlib.ocpVersionParam('BUILD_VERSION'),
                    commonlib.mockParam(),
                    booleanParam(
                        name: 'SWEEP_BUILDS',
                        defaultValue: true,
                        description: 'Sweep and attach builds to advisories',
                    ),
                ],
            ],
        ]
    )

    commonlib.checkMock()

    def version = params.BUILD_VERSION
    def major = version[0]
    doozerOpts = "--group openshift-${version}"
    stage("Init") {
        echo "Initializing bug sweep for ${version}. Sync: #${currentBuild.number}"
        currentBuild.displayName = "${version} bug sweep"

        buildlib.assertBuildPermitted(doozerOpts)

        buildlib.elliott "--version"
        sh "which elliott"

        buildlib.kinit()
    }

    currentBuild.description = "Repairing state and sweeping new bugs.\n"
    def kind = (major == '4') ? 'image' : 'rpm'

    stage("Repair bug state") {
        try {
            currentBuild.description += "* Moving attached bugs in MODIFIED state to ON_QA...\n"
            buildlib.elliott "--group=openshift-${version} repair-bugs --use-default-advisory ${kind} --auto" 
        } catch (elliottErr) {
            currentBuild.description = "Error repairing:\n${elliottErr}"
            throw elliottErr
        }
    }

    stage("Sweep bugs") {
        try {
            currentBuild.description += "* Searching for and attaching new bugs in MODIFIED state...\n"
            buildlib.elliott "--group=openshift-${version} find-bugs --mode sweep --use-default-advisory ${kind}"
        } catch (elliottErr) {
            currentBuild.description += "Error sweeping bugs:\n${elliottErr}"
            throw elliottErr
        }
    }
    stage("Sweep builds") {
        if (!params.SWEEP_BUILDS) {
            currentBuild.description += "* Not sweeping builds\n"
            return
        }
        try {
            currentBuild.description += "* Searching for and attaching new builds...\n"
            buildlib.elliott "-g openshift-${version} find-builds -k rpm --use-default-advisory rpm"
            if (major == '4') {
                buildlib.elliott "-g openshift-${version} --branch rhaos-${version}-rhel-8 find-builds -k rpm --use-default-advisory rpm"
            }
            buildlib.elliott "-g openshift-${version} find-builds -k image --use-default-advisory image"
        } catch (elliottErr) {
            currentBuild.description += "Error sweeping builds:\n${elliottErr}"
            throw elliottErr
        }
    }
    currentBuild.description = "Ran without errors\n---------------\n" + currentBuild.description
}
