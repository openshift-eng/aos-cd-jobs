#!/usr/bin/env groovy

node {
    checkout scm
    def joblib = load("build.groovy")
    def commonlib = joblib.commonlib
    def slacklib = commonlib.slacklib

    commonlib.describeJob("ocp4", """
        <h2>Build OCP 4.y components incrementally</h2>
        <b>Timing</b>: Usually run automatically from merge_ocp.
        Humans may run as needed. Locks prevent conflicts.

        In typical usage, scans for changes that could affect package or image
        builds and rebuilds the affected components.  Creates new plashets if
        the automation is not frozen or if there are RPMs that are built in this run,
        and runs other jobs to sync builds to nightlies, create
        operator metadata, and sweep bugs and builds into advisories.

        May also build unconditionally or with limited components.
    """)


    // Expose properties for a parameterized build
    properties(
        [
            disableResume(),
            buildDiscarder(
                logRotator(
                    artifactDaysToKeepStr: '30',
                    daysToKeepStr: '30')),
            [
                $class: 'ParametersDefinitionProperty',
                parameterDefinitions: [
                    commonlib.dryrunParam(),
                    commonlib.mockParam(),
                    commonlib.doozerParam(),
                    commonlib.ocpVersionParam('BUILD_VERSION', '4'),
                    string(
                        name: 'NEW_VERSION',
                        description: '(Optional) version for build instead of most recent\nor "+" to bump most recent version',
                        defaultValue: "",
                        trim: true,
                    ),
                    string(
                        name: 'ASSEMBLY',
                        description: 'The name of an assembly to rebase & build for. If assemblies are not enabled in group.yml, this parameter will be ignored',
                        defaultValue: "stream",
                        trim: true,
                    ),
                    string(
                        name: 'DOOZER_DATA_PATH',
                        description: 'ocp-build-data fork to use (e.g. test customizations on your own fork)',
                        defaultValue: "https://github.com/openshift-eng/ocp-build-data",
                        trim: true,
                    ),
                    booleanParam(
                        name: 'FORCE_BUILD',
                        description: 'Build regardless of whether source has changed',
                        defaultValue: false,
                    ),
                    choice(
                        name: 'BUILD_RPMS',
                        description: 'Which RPMs are candidates for building? "only/except" refer to list below',
                        choices: [
                            "all",
                            "only",
                            "except",
                            "none",
                        ].join("\n"),
                    ),
                    string(
                        name: 'RPM_LIST',
                        description: '(Optional) Comma/space-separated list to include/exclude per BUILD_RPMS (e.g. openshift,openshift-kuryr)',
                        defaultValue: "",
                        trim: true,
                    ),
                    choice(
                        name: 'BUILD_IMAGES',
                        description: 'Which images are candidates for building? "only/except" refer to list below',
                        choices: [
                            "all",
                            "only",
                            "except",
                            "none",
                        ].join("\n"),
                    ),
                    string(
                        name: 'IMAGE_LIST',
                        description: '(Optional) Comma/space-separated list to include/exclude per BUILD_IMAGES (e.g. logging-kibana5,openshift-jenkins-2)',
                        defaultValue: "",
                        trim: true,
                    ),
                    commonlib.suppressEmailParam(),
                    string(
                        name: 'MAIL_LIST_SUCCESS',
                        description: '(Optional) Success Mailing List\naos-cicd@redhat.com,aos-qe@redhat.com',
                        defaultValue: "",
                        trim: true,
                    ),
                    string(
                        name: 'MAIL_LIST_FAILURE',
                        description: 'Failure Mailing List',
                        defaultValue: [
                            'aos-art-automation+failed-ocp4-build@redhat.com'
                        ].join(','),
                        trim: true
                    ),
                    string(
                        name: 'SPECIAL_NOTES',
                        description: '(Optional) special notes to include in the build email',
                        defaultValue: "",
                        trim: true,
                    ),
                ]
            ],
        ]
    )

    commonlib.checkMock()

    currentBuild.description = ""
    try {

        sshagent(["openshift-bot"]) {
            // To work on private repos, buildlib operations must run
            // with the permissions of openshift-bot

            lock("github-activity-lock-${params.BUILD_VERSION}") {
                stage("initialize") { joblib.initialize() }
                buildlib.assertBuildPermitted(doozerOpts)
                stage("build RPMs") {
                    try {
                        joblib.stageBuildRpms()
                    } catch (err) {
                        currentBuild.result = 'FAILURE'
                    }
                }

                stage("build compose") {
                    sh "rm -rf ./artcd_working && mkdir -p ./artcd_working"

                    def cmd = [
                        "artcd",
                        "-v",
                        "--working-dir=./artcd_working",
                        "--config=./config/artcd.toml",
                        "ocp4:build-compose",
                        "--version=${version.stream}-${version.release}",
                        "--assembly=${params.ASSEMBLY}"
                    ]
                    if (joblib.buildPlan.buildRpms) {
                        cmd << "--build-rpms"
                    }

                    withCredentials([
                            string(credentialsId: 'art-bot-slack-token', variable: 'SLACK_BOT_TOKEN'),
                            string(credentialsId: 'redis-server-password', variable: 'REDIS_SERVER_PASSWORD'),
                            string(credentialsId: 'redis-host', variable: 'REDIS_HOST'),
                            string(credentialsId: 'redis-port', variable: 'REDIS_PORT')
                        ]) {
                        sh(script: cmd.join(' '), returnStdout: true)
                    }

                    def plashets_built_yaml = "${env.WORKSPACE}/plashet-working/plashets_built.yaml"
                    def exists = fileExists plashets_built_yaml

                    if (exists) {
                        plashets_built = readYaml(file: plashets_built_yaml)
                        mirrorPlashet = plashets_built['rhel-server-ose-rpms']
                        echo "mirrorPlashet: ${mirrorPlashet}"

                        if(mirrorPlashet) {
                            // public rhel7 ose plashet, if present, needs mirroring to /enterprise/ for CI
                            rpmMirror.plashetDirName = mirrorPlashet.plashetDirName
                            rpmMirror.localPlashetPath = mirrorPlashet.localPlashetPath
                        }
                    }
                }

                if ( params.ASSEMBLY == "stream" ) {
                    // Since plashets may have been rebuilt, fire off sync for CI. This will transfer
                    // RPMs out to mirror.openshift.com/enterprise so that they may be consumed through
                    // CI rpm mirrors.
                    build wait: false, propagate: false, job: '/scheduled-builds/sync-for-ci', parameters: [string(name: 'ONLY_FOR_VERSION', value: params.BUILD_VERSION)]

                    // Also trigger rhcos builds for the release in order to absorb any changes from plashets or
                    // RHEL which may have triggered our rebuild. If there are no changes to the RPMs, the build
                    // should exit quickly. If there are changes, the hope is that by the time our images are done
                    // building, RHCOS will be ready and build-sync will find consistent RPMs.
                    build wait: false, propagate: false, job: 'build%2Frhcos', parameters: [string(name: 'BUILD_VERSION', value: params.BUILD_VERSION)]
                }

                stage("update dist-git") {
                    withCredentials([string(credentialsId: 'gitlab-ocp-release-schedule-schedule', variable: 'GITLAB_TOKEN')]) {
                        joblib.stageUpdateDistgit()
                    }
                }
                stage("build images") {
                    withCredentials([string(credentialsId: 'openshift-bot-token', variable: 'GITHUB_TOKEN')]) {
                            joblib.stageBuildImages()
                    }
                }
            }
            stage("sync images") {
                if (buildlib.allImagebuildfailed) { return }
                joblib.stageSyncImages()
            }
            stage("mirror RPMs") {
                sh "rm -rf ./artcd_working && mkdir -p ./artcd_working"

                // Create artcd command
                def cmd = [
                    "artcd",
                    "-v",
                    "--working-dir=./artcd_working",
                    "--config=./config/artcd.toml",
                ]
                if (params.DRY_RUN) {
                    cmd << "--dry-run"
                }
                localPlashetPath = rpmMirror.localPlashetPath ? rpmMirror.localPlashetPath : ''
                cmd += [
                    "ocp4:mirror-rpms",
                    "--version=${version.stream}",
                    "--assembly=${params.ASSEMBLY}",
                    "--local-plashet-path=${localPlashetPath}"
                ]
                withCredentials([
                            aws(credentialsId: 's3-art-srv-enterprise', accessKeyVariable: 'AWS_ACCESS_KEY_ID', secretKeyVariable: 'AWS_SECRET_ACCESS_KEY'),
                            string(credentialsId: 'redis-server-password', variable: 'REDIS_SERVER_PASSWORD'),
                            string(credentialsId: 'redis-host', variable: 'REDIS_HOST'),
                            string(credentialsId: 'redis-port', variable: 'REDIS_PORT'),
                            string(credentialsId: 'art-bot-slack-token', variable: 'SLACK_BOT_TOKEN')
                        ]) {
                    retry(3) {
                        timeout(time: 60, unit: 'MINUTES') { // aws s3 sync has been observed to hang before
                            sh(script: cmd.join(' '), returnStdout: true)
                        }
                    }
                }
            }
            stage("sweep") {
                if (buildlib.allImagebuildfailed) { return }
                buildlib.sweep(params.BUILD_VERSION)
            }
        }
        stage("report success") { joblib.stageReportSuccess() }
    } catch (err) {

        if (!buildlib.isBuildPermitted(doozerOpts)) {
            echo 'Exiting because this build is not permitted: ${err}'
            return
        }

        if (params.MAIL_LIST_FAILURE.trim()) {
            commonlib.email(
                to: params.MAIL_LIST_FAILURE,
                from: "aos-team-art@redhat.com",
                subject: "Error building OCP ${params.BUILD_VERSION}",
                body:
"""\
Pipeline build "${currentBuild.displayName}" encountered an error:
${currentBuild.description}


View the build artifacts and console output on Jenkins:
    - Jenkins job: ${commonlib.buildURL()}
    - Console output: ${commonlib.buildURL('console')}

"""

            )
        }
        currentBuild.description += "<hr />${err}"
        currentBuild.result = "FAILURE"
        throw err  // gets us a stack trace FWIW
    } finally {
        commonlib.compressBrewLogs()
        commonlib.safeArchiveArtifacts([
            "doozer_working/*.log",
            "doozer_working/brew-logs.tar.bz2",
            "doozer_working/*.yaml",
            "doozer_working/*.yml",
        ])
        buildlib.cleanWorkdir(joblib.doozerWorking)
        buildlib.cleanWorkspace()
    }
}
