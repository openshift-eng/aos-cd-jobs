node {
    checkout scm
    def buildlib = load("pipeline-scripts/buildlib.groovy")
    buildlib.initialize(false)
    def commonlib = buildlib.commonlib

    // Expose properties for a parameterized build
    properties(
        [
            disableResume(),
            buildDiscarder(
                logRotator(
                    artifactDaysToKeepStr: '5',
                    daysToKeepStr: '5')),
            [
                $class: 'ParametersDefinitionProperty',
                parameterDefinitions: [
                    [
                        name: 'GROUP',
                        description: 'The ocp-build-data group to synchronize (e.g. openshift-4.3)',
                        $class: 'hudson.model.StringParameterDefinition',
                        defaultValue: ""
                    ],
                    [
                        name: 'REPOSYNC_DIR',
                        description: 'The directory under reposync to write to',
                        $class: 'hudson.model.StringParameterDefinition',
                        defaultValue: ""
                    ],
                    [
                        name: 'ARCH',
                        description: 'Architecture of repo to synchronize; must have full definition in group.yml for release',
                        $class: 'hudson.model.ChoiceParameterDefinition',
                        choices: [ 'x86_64', 'ppc64le', 's390x' ].join("\n"),
                        defaultValue: 'x86_64'
                    ],
                    [
                        name: 'REPO_TYPE',
                        description: 'Type of repos to sync',
                        $class: 'hudson.model.ChoiceParameterDefinition',
                        choices: "unsigned\nsigned",
                        defaultValue: 'unsigned'
                    ],
                    commonlib.suppressEmailParam(),
                    [
                        name: 'MAIL_LIST_FAILURE',
                        description: 'Failure Mailing List',
                        $class: 'hudson.model.StringParameterDefinition',
                        defaultValue: [
                            'aos-art-automation+failed-reposync@redhat.com',
                        ].join(',')
                    ],
                    commonlib.mockParam(),
                ]
            ],
        ]
    )

    currentBuild.displayName = "${GROUP} - ${ARCH}"
    REPOSYNC_BASE_DIR="/mnt/workspace/reposync"
    LOCAL_SYNC_DIR = "${REPOSYNC_BASE_DIR}/${REPOSYNC_DIR}"
    LOCAL_CACHE_DIR = "${REPOSYNC_BASE_DIR}_cache/${REPOSYNC_DIR}"

    MIRROR_TARGET = "use-mirror-upload.ops.rhcloud.com"
    MIRROR_RELATIVE_REPOSYNC = "reposync/${REPOSYNC_DIR}"
    MIRROR_ENTERPRISE_BASE_DIR = "/srv/enterprise"
    MIRROR_SYNC_DIR = "${MIRROR_ENTERPRISE_BASE_DIR}/${MIRROR_RELATIVE_REPOSYNC}"

    // doozer_working must be in WORKSPACE in order to have artifacts archived
    DOOZER_WORKING = "${WORKSPACE}/doozer_working"
    buildlib.cleanWorkdir(DOOZER_WORKING)

    try {
        lock('buildvm-yum') { // prevent simultaneous job runs from conflicting over yum lock
            sshagent(['openshift-bot']) {
                // To work on real repos, buildlib operations must run with the permissions of openshift-bot

                stage("sync repos to local") {
                    cacheDir = "${LOCAL_CACHE_DIR}_${ARCH}"

                    if ( ARCH == 'x86_64' ) {
                        // Match legacy location for x86_64
                        syncDir = "${LOCAL_SYNC_DIR}"
                    } else {
                        // Non x86_64 arch directories will have the arch as a suffix
                        syncDir = "${LOCAL_SYNC_DIR}_${ARCH}"
                    }

                    command = "--working-dir ${DOOZER_WORKING} --group ${GROUP}"
                    command += " beta:reposync --output ${syncDir}/ --cachedir ${cacheDir}/ --repo-type ${REPO_TYPE} --arch ${ARCH}"
                    buildlib.doozer command

                    /**
                     * A bug in doozer once caused only noarch RPMs to be synced to the mirrors. Prevent this
                     * by making sure a minimum number of arch-specific RPMs actually exist locally before
                     * pushing the updated content to the mirrors.
                     */

                    sanityCheckRes = commonlib.shell(
                            returnAll: true,
                            script: "find ${syncDir} -name '*.${ARCH}.rpm' | wc -l"
                    )

                    if(sanityCheckRes.stdout.trim().toInteger() < 50){
                        error("Did not find a sufficient number of arch specific RPMs; human checks required!")
                    }

                }

                stage("push to mirror") {
                    sh "ssh -o StrictHostKeyChecking=no ${MIRROR_TARGET} -- mkdir --mode 755 -p ${MIRROR_SYNC_DIR}"
                    sh "rsync -avzh --chmod=a+rwx,g-w,o-w --delete -e \"ssh -o StrictHostKeyChecking=no\" ${LOCAL_SYNC_DIR}/ ${MIRROR_TARGET}:${MIRROR_SYNC_DIR} "

                    timeout(time: 2, unit: 'HOURS') {
                        sh "ssh -o StrictHostKeyChecking=no ${MIRROR_TARGET} -- push.enterprise.sh -v ${MIRROR_RELATIVE_REPOSYNC}"
                    }
                }
            }
        }
    } catch (err) {
        commonlib.email(
            to: "${MAIL_LIST_FAILURE}",
            replyTo: "aos-team-art@redhat.com",
            from: "aos-art-automation@redhat.com",
            subject: "Error syncing v${REPOSYNC_DIR} repos",
            body: """Encountered an error while running OCP pipeline: ${err}

Jenkins job: ${env.BUILD_URL}
        """);

        currentBuild.result = "FAILURE"
        throw err
    } finally {
        commonlib.safeArchiveArtifacts(["doozer_working/*.log"])
    }
}
